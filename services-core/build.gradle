apply plugin: 'java-library'

// Re-apply runtime required subproject gradle scripts and classpaths,
// so that Maps SDK doesn't rely on them being served from the top level build.gradle
apply plugin: 'de.fuerstenau.buildconfig'
apply from: "../gradle/dependencies.gradle"
apply from: file("${rootDir}/gradle/artifact-settings.gradle")

buildscript {
  apply from: "../gradle/dependencies.gradle"

  repositories {
    maven { url 'https://plugins.gradle.org/m2' }
  }

  dependencies {
    classpath pluginDependencies.buildConfig
  }
}

configurations {
  testOutput
}

buildConfig {
  packageName = 'com.mapbox.core'
  version = project.ext.versionName
  buildConfigField 'String', 'GIT_REVISION', getGitRevision()
}

static def getGitRevision() {
  def cmd = "git rev-parse --short HEAD"
  def proc = cmd.execute()
  def ref = proc.text.trim()
  return ref
}

dependencies {

  // Gson
  api dependenciesList.gson

  // Retrofit
  api dependenciesList.retrofit
  api dependenciesList.retrofit2Gson

  // Annotations
  compileOnly dependenciesList.supportAnnotation

  // OkHttp
  api dependenciesList.okhttp3Logging

  // Test Dependencies
  testOutput sourceSets.test.output
}
