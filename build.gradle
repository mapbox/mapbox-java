buildscript {
  apply from: "${rootDir}/gradle/dependencies.gradle"

  repositories {
    maven { url 'https://plugins.gradle.org/m2' }
    google()
    mavenCentral()
    jcenter()
  }

  dependencies {

    dependencies.create(pluginDependencies.checkstyle) {
      transitive = false
    }
    classpath 'com.android.tools.build:gradle:2.3.3'
    classpath pluginDependencies.errorprone
    classpath pluginDependencies.sonarqube
    classpath pluginDependencies.buildConfig
  }
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
  options.compilerArgs += ['-Xlint:all', '-Xlint:unchecked', 'autovaluegson.defaultCollectionsToEmpty:true']
}

allprojects {
  repositories {
    maven { url 'https://plugins.gradle.org/m2' }
    google()
    mavenCentral()
    jcenter()
  }
  group = GROUP
  version = VERSION_NAME
}

subprojects {
  apply from: "${rootDir}/gradle/dependencies.gradle"
  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: 'net.ltgt.errorprone'

  // Fixes issue with test resources not being found inside Intellij
  idea {
    module {
      testOutputDir = file('build/resources/test')
    }
  }

  sourceCompatibility = "1.8"
  targetCompatibility = "1.8"

  dependencies {

    errorprone dependenciesList.errorprone

    // Test Dependencies
    testCompile dependenciesList.junit
    testCompile dependenciesList.hamcrestJunit
    testCompile dependenciesList.mockito
  }
}

apply from: "${rootDir}/gradle/sonarqube.gradle"
