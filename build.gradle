buildscript {
  apply from: "${rootDir}/gradle/dependencies.gradle"

  repositories {
    maven { url 'https://plugins.gradle.org/m2' }
    google()
    mavenCentral()
    jcenter()
  }

  dependencies {

    dependencies.create(pluginDependencies.checkstyle) {
      transitive = false
    }

    classpath pluginDependencies.errorprone
    classpath pluginDependencies.buildConfig
    classpath pluginDependencies.jacoco

    // These plugins are for publishing to Bintray
    classpath pluginDependencies.bintray
    classpath pluginDependencies.maven
    classpath pluginDependencies.artifactory
  }
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
  options.compilerArgs += ['-Xlint:all', '-Xlint:unchecked', 'autovaluegson.defaultCollectionsToEmpty:true']
}

allprojects {
  repositories {
    maven { url 'https://plugins.gradle.org/m2' }
    google()
    mavenCentral()
    jcenter()
    // For publishing to Bintray
    maven { url 'https://mapbox.bintray.com/mapbox' }
  }
}

subprojects {
  apply from: "${rootDir}/gradle/dependencies.gradle"
  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: 'net.ltgt.errorprone'

  // Fixes issue with test resources not being found inside Intellij
  idea {
    module {
      testOutputDir = file('build/resources/test')
    }
  }

  sourceCompatibility = JavaVersion.VERSION_1_7
  targetCompatibility = JavaVersion.VERSION_1_7

  dependencies {

    errorprone dependenciesList.errorprone

    // Test Dependencies
    testCompile dependenciesList.junit
    testCompile dependenciesList.hamcrestJunit
    testCompile dependenciesList.mockito
  }
}

def TESTABLE_MODULES = ["services",
                        "services-core",
                        "services-directions",
                        "services-geocoding",
                        "services-geojson",
                        "services-matching",
                        "services-matrix",
                        "services-optimization",
                        "services-route-tiles",
                        "services-speech",
                        "services-staticmap",
                        "services-tilequery",
                        "services-turf",
                        "services-directions-refresh",
                        "services-isochrone"]

def RELEASE_MODULES = ["services",
                       "services-core",
                       "services-geojson",
                       "services-turf"]

subprojects { subproject ->

  tasks.withType(Jar) { jarTask ->
    if (!jarTask.name.endsWith("sourcesJar")) {
      jarTask.exclude("**/*.java")
    }
  }

  if (TESTABLE_MODULES.contains(subproject.name)) {
    subproject.apply plugin: "com.vanniktech.android.junit.jacoco"
    subproject.apply from: "${rootDir}/gradle/jacoco.gradle"
    subproject.apply from: "${rootDir}/gradle/checkstyle.gradle"
  }

  if (RELEASE_MODULES.contains(subproject.name)) {
    subproject.apply from: "${rootDir}/gradle/gradle-bintray.gradle"
    subproject.apply from: "${rootDir}/gradle/dependencies-graph.gradle"
  }

  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  artifacts {
    archives sourcesJar
    archives javadocJar
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}