ext {
    mapboxArtifactGroupId = 'com.mapbox.mapboxsdk'
    mapboxArtifactId = project.hasProperty('POM_ARTIFACT_ID') ? project.property('POM_ARTIFACT_ID') : System.getenv('POM_ARTIFACT_ID')
    mapboxArtifactTitle = 'Mapbox Java SDK'
    mapboxArtifactDescription = project.hasProperty('POM_DESCRIPTION') ? project.property('POM_DESCRIPTION') : System.getenv('POM_DESCRIPTION')
    mapboxDeveloperName = 'Mapbox'
    mapboxDeveloperEmail = 'apps@mapbox.com'
    mapboxDeveloperOrganization = 'Mapbox'
    mapboxDeveloperOrganizationUrl = 'http://www.mapbox.com'
    mapboxDeveloperId = 'mapbox'
    mapboxArtifactUrl = 'https://github.com/mapbox/mapbox-java'
    mapboxArtifactVcsUrl = 'https://github.com/mapbox/mapbox-java.git'
    mapboxArtifactScmUrl = 'scm:git@github.com:mapbox/mapbox-java.git'
    mapboxArtifactLicenseName = 'The Apache Software License, Version 2.0'
    mapboxArtifactLicenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    versionName = getVersionName()
}

def getVersionName() {
    def cmd = "git describe --tag --abbrev=0"
    def version = cmd.execute([], project.rootDir).text.trim()
    return isSnapshot() ? getSnapshotVersion(version) : getReleaseVersion(version)
}

def isSnapshot() {
    return project.hasProperty("snapshot")
            ? project.property("snapshot").toBoolean()
            : false
}

private static def getReleaseVersion(String version) {
    return version.substring(1)
}

private static def getSnapshotVersion(String version) {
    def matcher = version =~ /^[v|V](\d+)\.(\d+)\.(\d+.*)/
    def major = (matcher[0][1] as Integer)
    def isPreRelease
    try {
        Integer.parseInt(matcher[0][3])
        isPreRelease = false
    } catch (NumberFormatException ignored) {
        isPreRelease = true
    }
    def minor = (matcher[0][2] as Integer)
    if (!isPreRelease) {
        minor += 1
    }
    return "${major}.${minor}.0-SNAPSHOT"
}
