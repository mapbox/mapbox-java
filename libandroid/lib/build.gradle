apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23

        // This should be updated together with gradle.properties
        // Last number is 0 for a SNAPSHOT, 1 for a release
        versionCode 2 * 1000 + 0 * 100 + 0 * 10 + 0
        versionName "2.0.0-SNAPSHOT"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    // Support libraries
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'

    // Mapbox Java Services (development)
    compile project(':libjava')

    // Mapbox Java Services (release)
//    compile ('com.mapbox.mapboxsdk:mapbox-java-services:1.2.1@jar') {
//        transitive=true
//    }

    // Testing
    testCompile 'junit:junit:4.12'
}

android.libraryVariants.all { variant ->
    def name = variant.name
    task "javadoc$name"(type: Javadoc) {
        description = "Generates javadoc for build $name"
        failOnError = false
        destinationDir = new File(destinationDir, variant.baseName)
        source = files(variant.javaCompile.source)
        classpath = files(variant.javaCompile.classpath.files) + files(android.bootClasspath)
        exclude '**/R.java', '**/BuildConfig.java'
    }
}

apply from: 'gradle-checkstyle.gradle'
apply from: 'gradle-mvn-push.gradle'
